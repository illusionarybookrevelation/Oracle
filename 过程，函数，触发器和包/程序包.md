# 程序包
#### 简介：
程序包由PL/SQL程序元素(如变量，类型)和匿名PL/SQL块(如游标)，命名PL/SQL块(如存储过程和函数)组成。程序包可以被整体加载到内存中，从而大大加快其组成部分的访问速度。实际上程序包对于用户来说并不陌生，在PL/SQL程序中使用的DBMS_OUTPUT.PUT_LINE语句就是程序包的一个具体应用。DBMS_OUTPUT是程序包，而PUT_LINE就是其中的一个存储过程。程序包通常由规范和包主体组成。

#### 程序包规范
规定了在程序包中可以使用哪些变量，类型，游标和子程序(指各种命名的PL/SQL块)，语法格式如下：
``` sql
create [or replace] package pack_name is
    [declare_variable];     -- 规范内声明的变量
    [declare_type];         -- 规范内声明的类型
    [declare_cursor];       -- 规范内定义的游标
    [declare_function]; -- 规范内声明的函数，仅定义参数和返回类型，不包括函数体
    [declare_procedure]; -- 规范内声明的存储过程，仅定义参数，不包括存储过程主体
end pack_name;
```
**举例**：
``` sql
create or replace package pack_emp is
  /*获取指定部门的平均工资*/
  function fun_avg_sal(num_deptno number) return number;
  /*按照指定比例上调指定职务的工资*/
  procedure pro_regulate_sal(var_job varchar2,num_proportion number);
end pack_emp;
```
上述代码，规范只声明头部，没有函数体和存储过程主体，只定义了规范的程序包还不可以被使用

#### 程序包主体
程序包主体包含了在规范中声明的游标，过程和函数实现代码，另外，也可以在程序包的主体中声明一些内部变量。程序包主体的名称必须与规范的名称相同，Oracle会将规范和主体结合在一起组成程序包，实现一起编译代码。创建程序包主体所使用的语句与规范不同，主体使用CREATE PACKAGE BODY语句，语法如下：
``` sql
create [or replace] package body pack_name is
    [inner_variable]       -- 程序包主体的内部变量
    [cursor_body]          -- 游标主体
    [function_title]       -- 从规范中引入的函数头部声明
    {begin
      fun_plsql;   -- PL/SQL语句，函数主要功能的实现，begin~end部分即函数的body
    [exception]
      [dowith_sentences;]   -- 异常处理语句
     end [fun_name];}
    [procedure_title]      -- 从规范中引入的存储过程头部声明
    {begin
      pro_plsql;   --PL/SQL语句，过程主要功能的实现，begin~end部分即过程的body
    [exception]
      [dowith_sentences;]
     end [pro_name];}
    ...
end [pack_name];
```
**举例**：
``` sql
create or replace package body pack_emp is
  function fun_avg_sal(num_deptno number) return number is
    num_avg_sal number;
  begin
    select avg(sal) into num_avg_sal from emp
    where deptno = num_deptno;
    return(num_avg_sal);
  exception
    when no_data_found then
      dbms_output.put_line('该部门编号不存在员工记录！');
    return(0);
  end fun_avg_sal;
  procedure pro_regulate_sal(var_job varchar2,num_proportion number) is
  begin
    update emp set sal = sal*(1+num_proportion)
    where job = var_job;
  end pro_regulate_sal;
end pack_emp;
```

#### 调用程序包，使用其中的函数和存储过程
``` sql
declare
  num_deptno emp.deptno%type;
  var_job emp.job%type;
  num_avg_sal emp.sal%type;
  num_proportion number;
begin
  num_deptno := 10;
  num_avg_sal := pack_emp.fun_avg_sal(num_deptno);
  dbms_output.put_line(num_deptno||'号部门的平均工资是：'||num_avg_sal);
  var_job := 'SALESMAN';
  num_proportion := 0.1;
  pack_emp.pro_regulate_sal(var_job,num_proportion);
end;
```
#### 删除程序包
``` sql
drop package pack_name;
```
