# 存储过程的参数
#### 简介：
参数是一种向程序单元输入和输出数据的机制，存储过程可以接受多个参数，参数模式包括IN,OUT和IN OUT共3种。

#### 1，IN模式参数
属于输入类型参数，参数值由调用访传入，并且只能被存储过程读取，关键字IN位于参数名称之后。
``` sql
create or replace procedure insert_dept(   -- 注意下面参数类型不能指定长度
  num_deptno in number,     -- 定义IN模式的变量，它存储部门编号
  var_ename in varchar2,    -- 定义IN模式的变量，它存储部门名称
  var_loc in varchar2
) is
begin
  /*向dept表中插入记录*/
  insert into dept values(num_deptno,var_ename,var_loc);
  commit;         -- 提交数据库
end insert_dept;
```

#### 2，向存储过程传入参数有以下3种方式
- (1)指定名称传递：需要指定参数名称，即参数名称在左侧，中间是赋值符号"=>"，右侧是参数值，语法如下：
  ``` sql
  pro_name(parameter1=>value1[,parameter2=>value2]...)
  ```
	- parameter1：参数名称。在传递参数值时，这个参数名称与存储过程中定义的参数顺序无关。
	- value1：参数值。在它的左侧不是常规的赋值符号"="，而是一种新的赋值符号"=>"。需要注意参数值的类型要与参数的定义类型兼容。
- (2)按位置传递参数：用户提供的参数值顺序必须与存储过程中定义的参数顺序相同。

- (3)混合方式传递：就是将两种方式结合到一起。

#### 3，指定名称方式
``` sql
begin
  insert_dept(var_ename=>'采购部',var_loc=>'成都',num_deptno=>15);
end;
```
可以看出，在定义存储过程中num_deptno在前，而在传递参数时，它最后传递，所以，"指定名称"传递方式的参数值和参数定义顺序无关，但与参数个数有关。

#### 4，按位置方式
``` sql
begin
  insert_dept(28,'工程部','洛阳');
end;
```
#### 5，混合方式
``` sql
begin
  insert_dept(38,var_loc=>'济南',var_ename=>'测试部');
end;
```
**注意**：
如果在某个位置使用指定名称方式传入参数值后，其后面的参数值也要使用指定名称传递，因为这种方式有可能已经破坏了参数原始的定义顺序。

---
#### 6，OUT模式参数
属于输出类型的参数，表示这个参数在存储过程中已经被赋值，并且这个参数值可以被传递到当前存储过程以外的环境中，关键字OUT位于参数名称之后。
``` sql
create or replace procedure select_dept(
  num_deptno in number,           -- 定义in模式变量，要求输入部门编号
  var_dname out dept.dname%type,  -- 定义OUT模式变量，可存储部门名称并输出
  var_loc out dept.loc%type
)is
begin
  select dname,loc into var_dname,var_loc
  from dept
  where deptno = num_deptno;    -- 检索某个部门编号的部门信息
exception
  when no_data_found then       -- 若SELECT语句无返回记录
    dbms_output.put_line('该部门编号不存在');   -- 输出信息
end select_dept;
```
**补充**：调用OUT模式的存储过程有以下2种方式
- (1)在PL/SQL块中的DECLARE部分定义与存储过程种OUT参数兼容的若干变量，以便接收OUT参数的返回值。
- (2)在SQL*Plus环境中使用VARIABLE关键字声明两个变量，用于存储OUT参数的返回值，执行EXEC命令。

#### 7，OUT模式的PL/SQL
``` sql
declare
  var_dname dept.dname%type;    -- 声明变量，对应过程中的OUT模式的var_dname
  var_loc dept.loc%type         -- 声明变量，对应过程中的OUT模式的var_loc
begin
  select_dept(99,var_dname,var_loc);-- 传入部门编号，然后输出部门名称和位置信息
  dbms_output.put_line(var_dname||'位于：'||var_loc);  -- 输出部门信息
end;
```
#### 8，OUT模式的EXEC命令
``` sql
SQL>variable var_dname varchar2(50);
SQL>variable var_loc varchar2(50);
SQL>exec select_dept(15,:var_dname.:var_loc);
```
**补充**：可以通过PRINT命令或SELECT语句来输出变量的值。
``` sql
SQL>print var_dname var_loc;
SQL>select var_dname,var_loc from dual;
```

---
#### 9，IN OUT模式参数
IN模式只能根据被传入的指定值(或默认值)为存储过程提供数据；OUT类型的参数只能等待被赋值。IN OUT则结合两者的特点，在调用存储过程时，可以从外界向该类型的参数传入值，在执行完存储过程后，可以将该参数的返回值传给外界。
``` sql
create or replace procedure pro_square(
  num in out number,        -- 计算它的平方或平方根，这是一个IN OUT参数
  flag in boolean) is       -- 计算它的平方或平方根的标识，这是一个IN参数
  i int := 2;               -- 表示计算平方，这是一个内部变量
begin
  if flag then              -- 若为TRUE
    num := power(num,i);    -- 计算平方
  else
    num := sqrt(num);       -- 计算平方根
  end if;
end;

declare
  var_number number;        -- 存储要进行运算的值和运算后的结果
  var_temp number;          -- 存储要进行运算的值
  boo_flag boolean;         -- 平方或平方根的逻辑标记
begin
  var_temp := 3;            -- 变量赋值
  var_number := var_temp;
  boo_flag := false;
  pro_square(var_number,boo_flag);   -- 调用存储过程
  if boo_flag then
    dbms_output.put_line(var_temp||'的平方是：'||var_number);
  else
    dbms_output.put_line(var_temp||'的平方根是：'||var_number);
  end if;
end;
```

#### 10，IN参数的默认值
Oracle支持在声明IN参数的同时给其初始化默认值，在调用存储过程时，如果没有向IN参数传入值，则可以使用默认值替代。
``` sql
create or replace procedure insert_dept(
  num_deptno in number,
  var_dname in varchar2 default '综合部',
  var_loc in varchar2 default '北京'
) is
begin
  insert into dept values(num_deptno,var_dname,var_loc);
end;

declare
  row_dept dept%rowtype;
begin
  insert_dept(57,var_loc=>'太原');
  commit;
  select * into row_dept from dept where deptno=57;
  dbms_output.put_line('部门名称是：《'||row_dept.dname||'》，位置是：《'||row_dept.loc||'》');
end;
```
上述代码，存储过程有3个IN参数，这里只传入两个参数，var_dname则是使用了定义的默认参数。

---
#### 11，删除存储过程
``` sql
/*DROP PROCEDURE count_num;*/
drop procedure insert_dept;
```
